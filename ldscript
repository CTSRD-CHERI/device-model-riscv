OUTPUT_ARCH("riscv")
ENTRY(_start)

SECTIONS
{
	. = kernbase;

	.start . : AT(kernbase - dmapbase)
	{
		*start*.o(.text)
	}

	.text : ALIGN(16) {
		*(.text)
	}

	.rodata : ALIGN(16) {
		*(.rodata)
	}

	.rodata.str1.1 : ALIGN(16) {
		*(.rodata.str1.1)
	}

	set_pci_devemu_set : ALIGN(16) {
		*(set_pci_devemu_set)
	}

	set_lpc_sysres_set : ALIGN(16) {
		*(set_lpc_sysres_set)
	}

	set_inout_port_set : ALIGN(16) {
		*(set_inout_port_set)
	}

	__cap_relocs : ALIGN(16) {
		*(__cap_relocs)
	}

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.sdata : ALIGN(16) {
		. = ALIGN(16);
		_sdata = ABSOLUTE(.);
		*(.sdata)
		. = ALIGN(16);
		__start_set_sysinit_set = ABSOLUTE(.);
		*(set_sysinit*)
		. = ALIGN(16);
		__stop_set_sysinit_set = ABSOLUTE(.);

		. = ALIGN(16);
		_edata = ABSOLUTE(.);
	}

	.bss : ALIGN(16) {
		_sbss = ABSOLUTE(.);
		*(.bss COMMON)
		*(.sbss)
		_ebss = ABSOLUTE(.);
	}
}
